-- =============================================
-- Author:		<Dexter Zafra>
-- Create date: <7/28/2008>
-- Description:	<Return article search result with custom paging and bidirectional dynamic sorting>
-- =============================================
CREATE PROCEDURE [dbo].[GetArticleSearchResult] 

@Search varchar(50),
@CATID int,
@OrderBy int = '',
@SortBy int = '',
@PageIndex int = 1,
@PageSize int = 20

AS
BEGIN

DECLARE @StartRow int
DECLARE @EndRow int

SET @StartRow = (@PageSize * (@PageIndex - 1))  + 1  
SET @EndRow = @PageSize * @PageIndex + 1

IF (@CATID = 0)
 BEGIN
      SET @CATID = Null
 END

SET NOCOUNT ON;

WITH ArticleSearch AS
(
    SELECT
       ROW_NUMBER() OVER 
         (
            -- Dynamic sorting
            ORDER BY 
                  CASE WHEN @OrderBy = 1 AND @SortBy = 1 THEN a.Hits END DESC,
                  CASE WHEN @OrderBy = 1 AND @SortBy = '' THEN a.Hits END DESC,
                  CASE WHEN @OrderBy = 1 AND @SortBy = 2 THEN a.Hits END ASC,
                  CASE WHEN @OrderBy = 2 AND @SortBy = 1 THEN a.No_Rates END DESC,
                  CASE WHEN @OrderBy = 2 AND @SortBy = '' THEN a.No_Rates END DESC,
                  CASE WHEN @OrderBy = 2 AND @SortBy = 2 THEN a.No_Rates END ASC,
                  CASE WHEN @OrderBy = 3 AND @SortBy = 2 THEN a.Title END ASC,
                  CASE WHEN @OrderBy = 3 AND @SortBy = '' THEN a.Title END ASC,
                  CASE WHEN @OrderBy = 3 AND @SortBy = 1 THEN a.Title END DESC,
                  CASE WHEN @OrderBy = 4 AND @SortBy = 1 OR @OrderBy = '' THEN a.Post_Date END DESC,
                  CASE WHEN @OrderBy = 4 AND @SortBy = '' OR @OrderBy = '' THEN a.Post_Date END DESC,
                  CASE WHEN @OrderBy = 4 AND @SortBy = 2 THEN a.Post_Date END ASC
         ) AS RowNumber, 
              a.ID,
              a.CAT_ID,
              b.CAT_NAME, 
              a.Title, 
              a.Post_Date, 
              a.Content, 
              a.Hits, 
              a.Author,
              a.Keyword,
              a.No_Rates,
              a.UID, 
              CAST((1.0 * a.No_Rating/a.No_Rates) AS DECIMAL(2,1)) AS Rates,
              dbo.GetSearchResultArticleCount(@Search, @CATID) AS RCount, 
              a.Summary,
              a.Total_Comments        
       FROM Cooking_Article a WITH (NOLOCK)
            INNER JOIN Cooking_Article_Category b WITH (NOLOCK)
       ON 
            a.CAT_ID = b.CAT_ID
       WHERE Show = 1 AND a.CAT_ID = COALESCE(@CATID, a.CAT_ID) AND a.Title Like '%' + @Search + '%'
)
-- Statement that executes the CTE
SELECT a.*
FROM
      ArticleSearch a
WHERE
      a.RowNumber BETWEEN @StartRow AND @EndRow - 1
ORDER BY
      a.RowNumber

END


		