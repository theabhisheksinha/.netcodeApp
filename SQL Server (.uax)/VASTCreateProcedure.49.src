-- =============================================
-- Author:		<Dexter Zafra>
-- Create date: <5/22/2008>
-- Description:	<Perform sorting>
-- =============================================
CREATE PROCEDURE [dbo].[GetSortingRecipe]

@Sid int,
@SortBy int = '',
@PageIndex int = 1,
@PageSize int = 20

AS
BEGIN

DECLARE @StartRow int
DECLARE @EndRow int

SET @StartRow = (@PageSize * (@PageIndex - 1))  + 1  
SET @EndRow = @PageSize * @PageIndex + 1

SET NOCOUNT ON;

WITH RecipeSorting AS
(
    SELECT TOP 100
       ROW_NUMBER() OVER 
         (
           -- Dynamic bidirectional sorting
            ORDER BY 
                  CASE WHEN @Sid = 1 AND @SortBy = 1 THEN HITS END DESC,
                  CASE WHEN @Sid = 1 AND @SortBy = '' THEN HITS END DESC,
                  CASE WHEN @Sid = 1 AND @SortBy = 2 THEN HITS END ASC,
                  CASE WHEN @Sid = 2 AND @SortBy = 1 THEN NO_RATES END DESC,
                  CASE WHEN @Sid = 2 AND @SortBy = '' THEN NO_RATES END DESC,
                  CASE WHEN @Sid = 2 AND @SortBy = 2 THEN NO_RATES END ASC,
                  CASE WHEN @Sid = 3 AND @SortBy = 2 THEN Name END ASC,
                  CASE WHEN @Sid = 3 AND @SortBy = '' THEN Name END ASC,
                  CASE WHEN @Sid = 3 AND @SortBy = 1 THEN Name END DESC,
                  CASE WHEN @Sid = 4 AND @SortBy = 1 OR @Sid = '' THEN Date END DESC,
                  CASE WHEN @Sid = 4 AND @SortBy = '' OR @Sid = '' THEN Date END DESC,
                  CASE WHEN @Sid = 4 AND @SortBy = 2 THEN Date END ASC
         ) AS RowNumber, 
              ID, 
              CAT_ID, 
              Category, 
              Name, 
              Author, 
              Date, 
              HITS, 
              RATING, 
              NO_RATES,
              [UID], 
              CAST((1.0 * RATING/NO_RATES) as decimal(2,1)) as Rates,
              COALESCE(NULLIF(RecipeImage, ''), 'noimageavailable.gif') as RecipeImage
    FROM
         dbo.Recipes WITH (NOLOCK)
    WHERE 
         LINK_APPROVED = 1
)
-- Statement that executes the CTE
SELECT a.*
FROM
      RecipeSorting a
WHERE
      a.RowNumber BETWEEN @StartRow AND @EndRow - 1
ORDER BY
      a.RowNumber

END


		